apply plugin: 'java'
apply plugin: 'war'

webAppDirName = 'web'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
}

dependencies {
providedCompile 'javax.enterprise:cdi-api:1.2'
providedCompile 'javax.servlet:servlet-api:3.+'
providedCompile 'com.sun.faces:jsf-api:2.2.+'
providedCompile 'com.sun.faces:jsf-impl:2.2.+'
providedCompile 'org.glassfish.web:el-impl:2.2'
providedCompile 'javax.validation:validation-api:1.0.0.GA'
compile 'org.primefaces:primefaces:5.1'
compile(
    'de.beyondjava:angularFaces-core:2.1.0',
    'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.5.0'
)

// coming soon:
// compile 'de.beyondjava:angularFaces-widgets:2.2'

}

war.doFirst {
    String contents = new File( 'web/empty.xhtml' ).getText( 'UTF-8' )

    //Replace the text
    contents = contents.replaceAll( 'Title', 'Welcome to BootsFaces' )
    Date buildDate = new Date()
    String hostname= InetAddress.getLocalHost().getHostName()
    String currentJvm = org.gradle.internal.jvm.Jvm.current()
	
	String dependencies="";
	try {
    	def compileConfiguration = project.configurations.getByName("compile")
    	def resolvedConfiguration = compileConfiguration.resolvedConfiguration
    	def resolvedArtifacts = resolvedConfiguration.resolvedArtifacts
    	resolvedArtifacts.each { dp ->
			dependencies += "${dp}<br>"
    	}
	} catch (Exception e) {
		dependencies = "Couldn't resolve the dependencies: $e"
	}
	
    String currentOS = System.properties['os.name']+' '+System.properties['os.version']+' '+System.properties['os.arch']
    String buildInfo = """
					   <b:panel title="Build Informations" look="info">
                           Built on $hostname $currentOS<br/>
                           by Gradle ${project.gradle.gradleVersion}<br/>
                           Java Version $currentJvm<br/>
                           sourceCompatibility $sourceCompatibility<br/>
                           targetCompatibility $targetCompatibility<br/>
                           Build Date  ${buildDate}<br/>
                       </b:panel>
					   <b:panel title="Libraries used in the demo" look="info">
                           $dependencies
                       </b:panel>
					   """
    contents = contents.replaceAll( '</h1>', '</h1>'+buildInfo )
    new File( 'web/index.xhtml' ).write( contents, 'UTF-8' )
}

war {
   archiveName = 'BootsFacesWeb.war'
   classpath fileTree(dir: 'bsflib', include: '*') // adds a file-set to the WEB-INF/lib dir.
}

